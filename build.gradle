buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'org.asciidoctor', name: 'asciidoctor-gradle-plugin', version: '1.5.6'
        classpath group: 'org.asciidoctor', name: 'asciidoctorj-pdf', version: '1.5.0-alpha.16'
        classpath group: 'org.kt3k.gradle.plugin', name: 'coveralls-gradle-plugin', version: '2.0.1'
        classpath group: 'com.jfrog.bintray.gradle', name: 'gradle-bintray-plugin', version: '1.6'
        classpath group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.0.0'
        classpath group: 'org.ajoberstar', name: 'gradle-git', version: '1.7.2'
    }
}

plugins {
    id 'java'
    id 'eclipse'
}
apply from: 'gradle/publishing.gradle'
apply from: 'gradle/coverage.gradle'
apply from: 'gradle/documentation.gradle'

group 'io.github.swagger2markup'
description = 'swagger2markup Build'
version = '1.3.4-SNAPSHOT'

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint:unchecked']
}

repositories {
    maven {
        url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
    }
    jcenter()
    //mavenLocal()
}

dependencies {
    implementation group: 'io.github.swagger2markup', name: 'markup-document-builder', version: '1.1.3-SNAPSHOT'
    implementation group: 'io.swagger', name: 'swagger-compat-spec-parser', version: '1.0.35'
    implementation group: 'org.apache.commons', name: 'commons-configuration2', version: '2.1'
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.2'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
    implementation group: 'io.vavr', name: 'vavr', version: '0.9.1'
    implementation group: 'ch.netzwerg', name: 'paleo-core', version: '0.11.0'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.asciidoctor', name: 'asciidoctorj', version: '1.5.6'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
    testImplementation group: 'io.github.robwin', name: 'assertj-diff', version: '0.1.1'
}

test {
    if (System.properties['http.proxyHost']) {
        systemProperty 'http.proxyHost', System.properties['http.proxyHost']
        systemProperty 'http.proxyPort', System.properties['http.proxyPort']
        systemProperty 'http.nonProxyHosts', System.properties['http.nonProxyHosts']
    }
    systemProperty 'file.encoding', 'UTF-8'
}

javadoc {
    options.encoding = 'UTF-8'
}
